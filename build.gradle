plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'com.modrinth.minotaur' version '2.8.+'
    id 'com.matthewprenger.cursegradle' version '1.4.+'
}

version = project.mod_version
group = project.maven_group

dependencies {
    // Change these versions in gradle.properties
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("*.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
    withSourcesJar()
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    versionName = "SnowyLeavesPlus ${version} for Minecraft ${project.minecraft_version}"
    versionNumber = "mc${project.minecraft_version}-${version}"
    projectId = "of7wIinq"
    uploadFile = remapJar
    versionType = "beta"
    loaders = ["fabric", "quilt"]
    gameVersions = [
        "1.19.4",
        "1.20",
        "1.20.1",
        "1.20.2",
        "1.20.3",
        "1.20.4",
        "1.20.5",
        "1.20.6",
        "1.21"
    ]
    syncBodyFrom = rootProject.file("README.md").text
    dependencies = [new ModDependency("fabric-api", "required")]
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN")
    project {
        id = "611998"
        releaseType = "beta"
        addGameVersion("Fabric")
        addGameVersion("Quilt")
        addGameVersion("1.19.4")
        addGameVersion("1.20")
        addGameVersion("1.20.1")
        addGameVersion("1.20.2")
        addGameVersion("1.20.3")
        addGameVersion("1.20.4")
        addGameVersion("1.20.5")
        addGameVersion("1.20.6")
        addGameVersion("1.21")
        mainArtifact(remapJar) {
            displayName = "SnowyLeavesPlus ${version} for Minecraft ${project.minecraft_version}"
        }
        relations { requiredDependency("fabric-api") }
    }
}
