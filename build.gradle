plugins {
    id 'fabric-loom' version "${loom_version}"
    // Check version: https://maven.firstdark.dev/#/releases/com/hypherionmc/modutils/modpublisher
    id "com.hypherionmc.modutils.modpublisher" version "2.1.+"
}

dependencies {
    // Change these versions in gradle.properties
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    filteringCharset "UTF-8"

    filesMatching("*.mod.json") {
        expand "version": project.mod_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = "${project.archives_base_name}-mc${project.minecraft_version}"
    withSourcesJar()
}

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        curseforge System.getenv("CURSEFORGE_TOKEN")
        github System.getenv("GITHUB_TOKEN")
    }
    setDebug(true)
    changelog = "Update to `1.21.6`"

    setCurseID("611998")
    setModrinthID("of7wIinq")

    setDisplayName("SnowyLeavesPlus ${project.mod_version} for Minecraft ${project.minecraft_version}")
    setVersion("${project.mod_version}-mc${project.minecraft_version}")
    setVersionType("beta")

    setLoaders("fabric", "quilt")
    setGameVersions("1.21.6")
    setGameVersions("1.21.5")

    setArtifact(remapJar)
    addAdditionalFile(remapSourcesJar)

    modrinthDepends {
        required 'fabric-api'
    }
    curseDepends {
        required 'fabric-api'
    }
}
